API description LakesideMutual 
data type Nothing D<void>

data type Address {"country":D<string>, 
    ("street":D<string>|"pobox":D<int>), "zip":D<int>, "city":D<string> }
data type PhoneNumber ("country":D<string>, "number":D<string>)
data type CustomerDTO <<Entity>> 
    {"id", "name", "addresses":Address*, "phoneNumbers":PhoneNumber*}
data type RefactoredCustomerDTO <<Entity>> {"id", "name", 
    <<Linked_Information_Holder>>"addresses":L*, "phoneNumbers":PhoneNumber*}
data type SuccessCode <<Annotated_Parameter_Collection>>(MD<bool>,L)

// syntactic sugar demo:
data type I <<Identifier_Element>> D<int> // int is a UUID or so
data type E <<Entity>> D
data type URI <<Link_Element>> D<string>
data type HateoasEnabledRecord {I, E, L, URI*}
data type RichHyperlinkForRESTMaturityLevel3 { 
	"target": URI, 
 	"relation_type": D,
 	"verb": D<string>, 
 	"request_message_schema": D}

endpoint type CustomerManagementTake1 serves as INFORMATION_HOLDER_RESOURCE
exposes 
  operation findCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload D<void> // no payload
    delivering payload "customerIDList":ID*
    
  operation readCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload "customerID":ID 
    delivering payload "customerDTO":D?

  operation updateCustomer with responsibility EVENT_PROCESSOR
    expecting payload "customerDTO":D?  
    delivering payload SuccessCode  

endpoint type CustomerManagementTake2 serves as INFORMATION_HOLDER_RESOURCE
exposes 
  operation findCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload Nothing 
    delivering payload <<Value>>"customerIDList":ID<int>*
    
  operation readCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload <<Value>>"customerIDList":ID<int>* 
    delivering payload CustomerDTO?

  operation updateCustomer with responsibility EVENT_PROCESSOR
    expecting payload CustomerDTO?  
    delivering payload SuccessCode  

endpoint type CustomerManagementTake3 version "0.3.0" serves as INFORMATION_HOLDER_RESOURCE

exposes 
  operation findCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload Nothing 
    delivering payload <<Value>>"customerIDList":ID<int>*
    
  operation readCustomer with responsibility RETRIEVAL_OPERATION
    expecting payload <<Value>>"customerIDList":ID<int>* 
    delivering payload RefactoredCustomerDTO?
    
  operation readAddress with responsibility RETRIEVAL_OPERATION
    expecting payload "addressId":L
    delivering payload Address

  operation updateCustomer with responsibility EVENT_PROCESSOR
    expecting payload CustomerDTO?  
    delivering payload SuccessCode  
    
    
API provider LakesideMutalCorporateIT
 offers CustomerManagementTake3 
 at endpoint location "Some URI address"
 endpoint governance EXPERIMENTAL_PREVIEW
 under conditions "Some Ts and Cs"
 
// IPA