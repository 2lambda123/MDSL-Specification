API description SpreadSheetExchangeAPI

data type CSVSpreadsheet CSVSheetTab*
data type CSVSheetTab {"name": V<string>, 
                       "content": Rows*}
data type Rows {"line": ID<int>, 
                "columns":Column+}
data type Column {"position": ID<string>, 
                  "header": V<string>?, 
                  <<Entity>> "cell": Cell}
data type Cell {"text":V<string> 
               | "intValue": V<int> 
               | "longValue": V<long> 
               | "formula": V<string>}
               
// TODO remodel with choice and subtypes
data type OASSecuritySchemeObject {
	// remodeling https://github.com/OAI/OpenAPI-Specification/blob/master/versions/3.0.2.md#securitySchemeObject:
	"type":ID, 
	"description": V<string>? default is "Some OASSecuritySchemeObject", 	
	"name": V<string>, 
	"in": V<string>, 
	"scheme": V<string>, 
	"bearerFormat": V<string>, 
	"flows": OAuthFlowsObject,
	"openIdConnectUrl":V<string>}

data type OAuthFlowsObject {
	"implicit": OAuthFlowsObject,
	"password": OAuthFlowsObject,
	"clientCredientials": OAuthFlowsObject,
	"authorizationCode": OAuthFlowsObject
}

data type OAuthFlowObject {
	"authorizationUrl": V<string>,
	"tokenUrl": V<string>,
	"refreshUrl": V<string>,
	"scopes": {"name": V<string>, "shortDescription": V<string>}
}

// data type OASSecurityObject {"type":ID, "credential": V<string>, "whatever":P}

data type GenericDomainEvent { ID, "dateAndTime", "content":V?}
data type SpreadsheetChanged { "genercDomainEvent": GenericDomainEvent, "content":CSVSpreadsheet}

endpoint type SpreadSheetExchangeEndpoint serves as TRANSFER_RESOURCE
 protected by policy OASSecuritySchemeObject
 exposes 
  operation uploadSpreadsheet with responsibility EVENT_PROCESSOR
    expecting payload CSVSpreadsheet
    delivering payload {"successFlag":V<bool>, ID}
    
  operation downloadSpreadsheet with responsibility RETRIEVAL_OPERATION
    expecting payload ID 
    delivering payload CSVSpreadsheet
      reporting error "SheetNotFound" 

channel type EventSourcingDemoTopic serves as TRANSFER_RESOURCE      
protected by policy OASSecuritySchemeObject
 exposes
  operation pubSubSampleChannelMessage with responsibility EVENT_PROCESSOR
   delivering payload "someEvent": GenericDomainEvent
  protected by policy OASSecuritySchemeObject
 
  event caclulationUpdated with responsibility EVENT_PROCESSOR
   delivering payload SpreadsheetChanged
  protected by policy OASSecuritySchemeObject

     
API provider SpreadSheetExchangeAPIProvider
offers SpreadSheetExchangeEndpoint
at endpoint location "Test"
via protocol AMQP

Message sender SampleTopicPublisher1
publishes EventSourcingDemoTopic
at endpoint location "TopicName"
via protocol Kafka

API client SpreadSheetExchangeAPIClient
consumes SpreadSheetExchangeEndpoint

Message receiver SampleTopicSubscriber1
subscribes EventSourcingDemoTopic
from SampleTopicPublisher1
via protocol Kafka

Message receiver SampleTopSubscriber2
subscribes EventSourcingDemoTopic
from SampleTopicPublisher1
via protocol Kafka

// TODO gateways and pub sub channels/topics
API gateway SpreadSheetExchangeAPIGateway 
offers SpreadSheetExchangeEndpoint at endpoint location "tbd" 
via protocol RESTful_HTTP 
  mapping default body
  mapping path parameters (id) 
  mapping query parameters (id, id2) 
  mapping form parameters (id, id2 , id3)
consumes SpreadSheetExchangeEndpoint 
from SpreadSheetExchangeAPIProvider
via protocol AMQP
mediates from Cell to Column // just a fake PoC, does not make sense (could do CSV vs. raw) 