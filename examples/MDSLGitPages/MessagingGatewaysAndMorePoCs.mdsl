API description SpreadSheetExchangeAPI

data type CSVSpreadsheet CSVSheetTab*
data type CSVSheetTab {"name": D<string>, 
                       "content": Rows*}
data type Rows {"line": ID<int>, 
                "columns":Column+}
data type Column {"position": ID<string>, 
                  "header": D<string>?, 
                  <<Entity>> "cell": Cell}
data type Cell {"text":D<string> 
               | "intValue": D<int> 
               | "longValue": D<long> 
               | "formula": D<string>}
               
// TODO remodel with choice and subtypes
data type OASSecuritySchemeObject {
	// remodeling https://github.com/OAI/OpenAPI-Specification/blob/master/versions/3.0.2.md#securitySchemeObject:
	"type":ID, 
	"description": D<string>? default is "Some OASSecuritySchemeObject", 	
	"name": D<string>, 
	"in": D<string>, 
	"scheme": D<string>, 
	"bearerFormat": D<string>, 
	"flows": OAuthFlowsObject,
	"openIdConnectUrl":D<string>}

data type OAuthFlowsObject {
	"implicit": OAuthFlowsObject,
	"password": OAuthFlowsObject,
	"clientCredientials": OAuthFlowsObject,
	"authorizationCode": OAuthFlowsObject
}

data type OAuthFlowObject {
	"authorizationUrl": D<string>,
	"tokenUrl": D<string>,
	"refreshUrl": D<string>,
	"scopes": {"name": D<string>, "shortDescription": D<string>}
}

// data type OASSecurityObject {"type":ID, "credential": D<string>, "whatever":P}

data type GenericDomainEvent { ID, "dateAndTime", "content":D?}
data type SpreadsheetChanged { "genercDomainEvent": GenericDomainEvent, "content":CSVSpreadsheet}

endpoint type SpreadSheetExchangeEndpoint serves as TRANSFER_RESOURCE
 protected by policy OASSecuritySchemeObject
 exposes 
  operation uploadSpreadsheet with responsibility EVENT_PROCESSOR
    expecting payload CSVSpreadsheet
    delivering payload {"successFlag":D<bool>, ID}
    
  operation downloadSpreadsheet with responsibility RETRIEVAL_OPERATION
    expecting payload ID 
    delivering payload CSVSpreadsheet
      reporting error "SheetNotFound" 

channel type EventSourcingDemoTopic serves as TRANSFER_RESOURCE      
protected by policy OASSecuritySchemeObject
 exposes
  operation pubSubSampleChannelMessage with responsibility EVENT_PROCESSOR
   delivering payload "someEvent": GenericDomainEvent
  protected by policy OASSecuritySchemeObject
 
  event caclulationUpdated with responsibility EVENT_PROCESSOR
   delivering payload SpreadsheetChanged
  protected by policy OASSecuritySchemeObject

     
API provider SpreadSheetExchangeAPIProvider
offers SpreadSheetExchangeEndpoint
at endpoint location "Test"
via protocol AMQP

Message sender SampleTopicPublisher1
publishes EventSourcingDemoTopic
at endpoint location "TopicName"
via protocol Kafka

API client SpreadSheetExchangeAPIClient
consumes SpreadSheetExchangeEndpoint

Message receiver SampleTopicSubscriber1
subscribes EventSourcingDemoTopic
from SampleTopicPublisher1
via protocol Kafka

Message receiver SampleTopSubscriber2
subscribes EventSourcingDemoTopic
from SampleTopicPublisher1
via protocol Kafka

// TODO gateways and pub sub channels/topics
API gateway SpreadSheetExchangeAPIGateway 
offers SpreadSheetExchangeEndpoint at endpoint location "tbd" 
via protocol RESTful_HTTP 
  mapping default body
  mapping path parameters (id) 
  mapping query parameters (id, id2) 
  mapping form parameters (id, id2 , id3)
consumes SpreadSheetExchangeEndpoint 
from SpreadSheetExchangeAPIProvider
via protocol AMQP
mediates from Cell to Column // just a fake PoC, does not make sense (could do CSV vs. raw) 