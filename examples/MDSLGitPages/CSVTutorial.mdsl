API description SpreadSheetExchangeAPI

data type CSVSpreadsheet CSVSheetTab*
data type CSVSheetTab {"name": D<string>, 
                       "content": Rows*}
data type Rows {"line": ID<int>, 
                "columns":Column+}
data type Column {"position": ID<string>, 
                  "header": D<string>?, 
                  <<Entity>> "cell": Cell}
data type Cell {"text":D<string> 
               | "intValue": D<int> 
               | "longValue": D<long> 
               | "formula": D<string>}

endpoint type SpreadSheetExchangeEndpoint
exposes 
  operation uploadSpreadsheet
    expecting payload CSVSpreadsheet
    delivering payload {"successFlag":D<bool>, ID}
    
  operation downloadSpreadsheet
    expecting payload ID
    delivering payload CSVSpreadsheet
      reporting error "SheetNotFound" 

API provider SpreadSheetExchangeAPIProvider
offers SpreadSheetExchangeEndpoint

API client SpreadSheetExchangeAPIClient
consumes SpreadSheetExchangeEndpoint

API gateway SpreadSheetExchangeAPIGateway 
offers SpreadSheetExchangeEndpoint at endpoint location "tbd" 
via protocol HTTP 
  /* 
  TODO update to V4.1 binding syntax:
  mapping default body
  mapping path parameters (id) 
  mapping query parameters (id, id2) 
  mapping form parameters (id, id2 , id3) 
 */
consumes SpreadSheetExchangeEndpoint 
from SpreadSheetExchangeAPIProvider
via protocol AMQP
mediates from Cell to Column // just a fake PoC, does not make sense (could do CSV vs. raw)